panuntunang pibonasyi(n, n1 = 1, n2 = 1)
  kapag n <= 2
    [0, n1, n2][n]
  kung_hindi
    sagot = wala
    (n-2).ulit na_ganito
      sagot = n1 + n2
      n1 = n2
      n2 = sagot
    katapusan
    sagot
  katapusan
katapusan

panuntunang pibonasyingPagsuksok(n, n1 = 1, n2 = 1)
  kapag n <= 2
    [0, n1, n2][n]
  kung_hindi
    ([wala].umikot.kumuha(n-2).isuksok([n1, n2]) na_ganito |nakaraan, x|
      nakaraan.magbagsak(1).itulak(nakaraan[0] + nakaraan[1])
    katapusan).huli
  katapusan
katapusan

panuntunang pibonasyingInuulit(n, n1 = 1, n2 = 1)
  kapag n <= 2
    binhi = [0, n1, n2]
    binhi[n]
  kung_hindi
    pibonasyingInuulit(n-1, n1, n2) + pibonasyingInuulit(n-2, n1, n2)
  katapusan
katapusan

panuntunang pibonasyingBuntutangInuulit(n, n1 = 1, n2 = 1)
  magbigay n1 kapag n <= 1
  magbigay n2 kapag n == 2
  magbigay pibonasyi(n - 1, n2, n1 + n2)
katapusan

panuntunang pibonasyingPaguulit(n, n1 = 1, n2 = 1)
  (2..n).isaisahin {
    n1, n2 = n2, n1 + n2
  }
  magbigay n1
katapusan

panuntunang pibonasyingPaguulitNaUmiikot(n, n1 = 1, n2 = 1)
  para_sa i sa 2..n
    n1, n2 = n2, n1 + n2
  katapusan
  magbigay n1
katapusan

isulat "Gamit ang Panuntunang Pibonasyi sa 10 ay #{pibonasyi 10}"
isulat "Gamit ang Panuntunang Pibonasying May Pagsuksok sa 9 ay #{pibonasyingPagsuksok 9}"
isulat "Gamit ang Panuntunang Pibonasying Inuulit sa 8 ay #{pibonasyingInuulit 8}"
isulat "Gamit ang Panuntunang Pibonasying May Buntot na Inuulit sa 7 ay #{pibonasyingBuntutangInuulit 7}"
isulat "Gamit ang Panuntunang Pibonasying Paguulit sa 6 ay #{pibonasyingPaguulit 6}"
isulat "Gamit ang Panuntunang Pibonasying Paguulit na Umiikot sa 5 ay #{pibonasyingPaguulitNaUmiikot 5}"